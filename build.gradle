import org.jetbrains.kotlin.konan.target.HostManager

apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "com.moowork.gradle:gradle-node-plugin:$gradle_node_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

ext.configuredVersion = project.hasProperty("releaseVersion") ? project.releaseVersion : project.version
ext.globalM2 = "$buildDir/m2"
ext.publishLocal = project.hasProperty("publishLocal")

apply from: "gradle/experimental.gradle"

def platforms = [
        "keccak": ["common", "jvm"],
        "ripemd160": ["common", "jvm"],
        "sha256": ["common", "jvm", "js", "darwin", "posix"]
]
def projectNeedsPlatform(project, platform) {
    def files = project.projectDir.listFiles()
    def hasPosix = files.any { it.name == "posix" }
    def hasDarwin = files.any { it.name == "darwin" }

    if (hasPosix && hasDarwin) return false

    if (hasPosix && platform == "darwin") return false
    if (hasDarwin && platform == "posix") return false
    if (!hasPosix && !hasDarwin && platform == "darwin") return false

    return files.any { it.name == "common" || it.name == platform }
}

allprojects {
    group = "com.github.komputing.khash"
    version = configuredVersion
    project.ext.hostManager = new HostManager()

    repositories {
        jcenter()
        mavenLocal()
        maven { url 'https://jitpack.io' }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }

    apply plugin: "kotlin-multiplatform"
    apply from: rootProject.file("gradle/utility.gradle")

    platforms.findAll { platformEntry -> platformEntry.key == project.name }
            .collect { it.value }.flatten()
            .each { platform ->
                if (projectNeedsPlatform(project, platform)) {
                    configure([it]) {
                        apply from: rootProject.file("gradle/${platform}.gradle")
                    }
                }
            }

    apply from: rootProject.file('gradle/dokka.gradle')
    apply from: rootProject.file('gradle/publish.gradle')

    configurations { testOutput }

    kotlin {
        configure(sourceSets) {
            def srcDir = name.endsWith("Main") ? "src" : "test"
            def resourcesPrefix = name.endsWith("Test") ? "test-" : ""
            def platform = name[0..-5]

            kotlin.srcDirs = ["$platform/$srcDir"]
            resources.srcDirs = ["$platform/${resourcesPrefix}resources"]

            languageSettings {
                progressiveMode = true
                experimentalAnnotations.each { useExperimentalAnnotation(it) }
            }
        }
    }
}

afterEvaluate {
    def allCompileKotlinTasks = subprojects.collect {
        it.hasProperty("compileKotlinJvm") ? [it.compileKotlinJvm] : []
    }.flatten()

    task dokkaWebsite(type: dokka.getClass()) {
        outputFormat = 'kotlin-website'
        outputDirectory = "${rootProject.projectDir}/apidoc"

        kotlinTasks { allCompileKotlinTasks }

        reportUndocumented = false
    }
}